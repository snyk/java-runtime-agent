plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'java'
    id 'application'
}

mainClassName = 'io.snyk.agent.bp.Trace'

dependencies {
    compile project(':common')
    compile files(find_tools_jar())
    compile 'org.apache.httpcomponents:fluent-hc:4.5.6'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.6'
    compile 'com.google.guava:guava:26.0-jre'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.2.0'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
}

// try and guess where tools.jar is today
static def find_tools_jar() {
    def home = System.getProperty('java.home')
    if (home.endsWith("jre")) {
        home += "/.."
    }

    home += "/lib/tools.jar"

    assert new File(home).isFile()

    home
}

task tools_jar {
    doLast {
        println find_tools_jar()
    }
}

shadowJar {
    mergeServiceFiles()

    classifier = null
    version = null
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
    jcenter()
}

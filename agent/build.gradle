plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'me.champeau.gradle.jmh' version '0.4.7'
    id 'java'
}

dependencies {
    // JMH requires 5.2, not 6.2.1 here. Probably a bug in their dependency management.
    compile 'org.ow2.asm:asm-tree:6.2.1'
    testCompile 'org.apache.commons:commons-text:1.4'
    testCompile 'com.google.code.gson:gson:2.8.5'
    testCompile 'com.google.guava:guava:26.0-jre'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.2.0'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
    jmh 'com.google.guava:guava:26.0-jre'
}

jar {
    manifest {
        attributes 'Premain-Class': 'io.snyk.agent.jvm.EntryPoint'
        attributes 'Can-Retransform-Classes': true
    }
}

shadowJar {
    mergeServiceFiles()

    classifier = null
    version = null
}

test {
    useJUnitPlatform()
}

jmh {
    // severely limit the time benchmarks take to run
    fork = 1
    iterations = 1
    warmupIterations = 1

    // allow us to use test classes during the benchmark, hopefully not rewritten
    includeTests = true
}

repositories {
    mavenCentral()
    jcenter()
}

wrapper {
    gradleVersion = "4.9"
    distributionType = Wrapper.DistributionType.ALL
}
